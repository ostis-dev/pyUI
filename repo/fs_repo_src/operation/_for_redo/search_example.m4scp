
/*
-----------------------------------------------------------------------------
This source file is part of OSTIS (Open Semantic Technology for Intelligent Systems)
For the latest info, see http://www.ostis.net

Copyright (c) 2010 OSTIS

OSTIS is free software: you can redistribute it and/or modify
it under the terms of the GNU Lesser General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

OSTIS is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU Lesser General Public License for more details.

You should have received a copy of the GNU Lesser General Public License
along with OSTIS.  If not, see <http://www.gnu.org/licenses/>.
-----------------------------------------------------------------------------
*/

/*
Операция поиска приемра
Условие инициализации:

Автор: Житко Владимир Александрович
Дата: 22 мая 2010
*/

#include "scp_keynodes.scsy"
#include "ui_keynodes.scsy"

procedure(search_example_ckeck,
[[
	k_question = "/seb/lingvistic/поиск примера";
	ge = "/seb/planimetry/Геометрия Евклида";
	concept1 = "/seb/planimetry/понятие";
	concept2 = "/seb/planimetry/множество";
]],
[{
	question, result,
	arc1, node1
}],
{[
	1_: in_: question,
	2_: out_: result
]}
)
printNl([ 1_: /"Check operation EXAMPLE"/])

searchElStr3([
	1_: fixed_: k_question,
	2_: assign_: arc_: pos_: const_: arc1,
	3_: fixed_: question
	],,is_not_my)

searchElStr3([
	1_: fixed_: question,
	2_: assign_: arc_: pos_: const_: arc1,
	3_: assign_: node_: const_: node1
	],,is_not_my)
	
searchElStr3([
	1_: fixed_: concept1,
	2_: assign_: arc_: pos_: const_: arc1,
	3_: fixed_: node1
	],is_my)
	
searchElStr3([
	1_: fixed_: concept2,
	2_: assign_: arc_: pos_: const_: arc1,
	3_: fixed_: node1
	],is_my)
	
nop(is_not_my)
	
label(is_my)
	
varAssign([1_: assign_: result, 2_: node1])
printNl([ 1_: /"OK"/])

label(is_not_my)

return()
end

procedure(search_example_oper,
[[
	f_search = "/operation/search_example/search_example";
]],
[{
	input, result_set
}],
{[
	1_: in_: input,
	2_: out_: result_set
]}
)

printNl([ 1_: /"Run operation"/])

genEl([1_: assign_: node_: const_: result_set])

callReturn([
	1_: fixed_: f_search,
	2_: fixed_: {[
		1_: input,
		2_: result_set
	]}
], e)

printNl([ 1_: /"End operation"/])

return()
end

procedure(search_example_ui,
[[
	f_search = "/operation/search_example/search_example";
	ui_Output;		// процедура вывода конструкций пользователю
]],
[{
	wnd, selected_el, sheet, shift, 
	arc, target, result_set, e, answer_reciever
}],
{[
	1_: in_: selected_el,
	2_: in_: sheet,
	3_: in_: answer_reciever
]}
)
//# Получение целевого объекта
searchElStr3([
	1_: fixed_: selected_el,
	2_: assign_: arc_: pos_: const_: arc,
	3_: assign_: node_: target
	])
	
genEl([
	1_: assign_: node_: const_: result_set
	])

callReturn([
	1_: fixed_: f_search,
	2_: fixed_: {[
		1_: fixed_: target,
		2_: fixed_: result_set
		]}
	], e)

callReturn([ 
	1_: fixed_: ui_Output,
	2_: fixed_: {[
		1_: answer_reciever,
		2_: result_set
	]}
	])

return()
end


procedure(search_example,
[[	
	"пример_" = "/seb/planimetry/пример_";
	min_semantic = "/operation/search_min_semantic/search_min_semantic";
]],
[{
	target, result_set,
	e, 
	node1, node2, 
	arc1, arc2, arc3, 
	set1, set2	
}],
{[
	1_: in_: target,
	2_: in_: out_: result_set
]}
)

searchSetStr5([
	1_: fixed_: target,
	2_: assign_: arc_: pos_: const_: arc1,
	3_: assign_: node_: const_: node1,
	4_: assign_: arc_: pos_: const_: arc2,
	5_: fixed_: "пример_",
	set3_: assign_: set1
	],,end_search_example,end_search_example)
	
label(beg_search_example)

	searchElStr3([
		1_: fixed_: set1,
		2_: assign_: arc_: pos_: const_: arc1,
		3_: assign_: node_: const_: node1
		],,end_search_example)
		
	eraseEl([1_: fixed_: arc1])
	
	searchElStr5([
		1_: fixed_: target,
		2_: assign_: arc_: pos_: const_: arc1,
		3_: fixed_: node1,
		4_: assign_: arc_: pos_: const_: arc2,
		5_: fixed_: "пример_"
		])
		
	genElStr3([1_: fixed_: result_set, 2_: assign_: arc_: pos_: const_: arc3, 3_: fixed_: target])
	genElStr3([1_: fixed_: result_set, 2_: assign_: arc_: pos_: const_: arc3, 3_: fixed_: arc1])
	genElStr3([1_: fixed_: result_set, 2_: assign_: arc_: pos_: const_: arc3, 3_: fixed_: node1])
	genElStr3([1_: fixed_: result_set, 2_: assign_: arc_: pos_: const_: arc3, 3_: fixed_: arc2])
	genElStr3([1_: fixed_: result_set, 2_: assign_: arc_: pos_: const_: arc3, 3_: fixed_: "пример_"])
	
	callReturn([
		1_: fixed_: min_semantic,
		2_: fixed_: {[
			1_: fixed_: node1,
			2_: fixed_: result_set
			]}
		], e, beg_search_example)
	
label(end_search_example)

return()
end























